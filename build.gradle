plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.0.1'
    id "org.javamodularity.moduleplugin" version "1.8.15"
}

group 'net.cassite'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs += '--enable-preview'
}
tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
    jvmArgs += '--enable-native-access=io.vproxy.pni'
    jvmArgs += '--enable-native-access=io.vproxy.windivert'
}

application {
    mainModule = 'net.cassite.hottapcassistant'
    mainClass = 'net.cassite.hottapcassistant.Main'
}

run {
    jvmArgs += '--add-opens=javafx.graphics/javafx.scene.text=io.vproxy.vfx'
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media']
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'com.github.kwhat:jnativehook:2.2.2'
    implementation group: 'io.vproxy', name: 'vjson', version: '1.5.4-jdk9'
    implementation group: 'io.vproxy', name: 'vfx', version: '1.4.0'
    implementation group: 'net.cassite', name: 'xboxrelay-ui', version: '1.0.0'
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.12.1'
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.12.1'
    implementation group: 'io.vertx', name: 'vertx-core', version: '4.3.7'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation group: 'io.projectreactor.tools', name: 'blockhound', version: '1.0.6.RELEASE'
    implementation group: 'net.cassite', name: 'tof-pcap-java', version: '1.0.0'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.1.2'
    implementation group: 'io.vproxy', name: 'commons', version: '1.4.0'
    implementation group: 'io.vproxy', name: 'divert-java-core', version: '1.1.1'
    implementation group: 'io.vproxy', name: 'divert-java-hostsmanager', version: '1.1.1'
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/幻塔PC助手-${javafx.platform.classifier}.zip")
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
        jvmArgs = ['-Xmx320m', '-Djava.library.path=%TEMP%', '-Dio.vproxy.vfx.logLevel=INFO',
                   '--enable-preview', '--enable-native-access=ALL-UNNAMED',
                   '--enable-native-access=io.vproxy.pni',
                   '--enable-native-access=io.vproxy.windivert',
                   '--add-opens=javafx.graphics/javafx.scene.text=io.vproxy.vfx',
        ]
        noConsole = true
    }
    mergedModule {
        additive = true
        uses 'io.vertx.core.spi.VertxServiceProvider'
        uses 'io.vertx.core.spi.VerticleFactory'
        uses 'io.vertx.core.spi.JsonFactory'
        uses 'io.vertx.core.spi.launcher.CommandFactory'
        forceMerge 'kotlin'
        forceMerge 'slf4j'
    }
}

tasks.jlinkZip.doFirst {
    copy {
        from("docs") {
            include "如何运行-How-to-Run.txt"
        }
        from("misc") {
            include "launch.bat"
            include "launch-with-pwsh.bat"
        }
        into "${buildDir}/image/"
    }
}

jlinkZip {
    group = 'distribution'
}

task runTest(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "net.cassite.hottapcassistant.test.TestMain"
    systemProperty("IsDebug", System.getProperty("IsDebug", "false"))
    args = ["net.cassite.hottapcassistant.test." + System.getProperty("TestClass")]
}
