plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

group 'net.cassite'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'net.cassite.hottapcassistant'
    mainClass = 'net.cassite.hottapcassistant.Main'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.swing']
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.31"
    implementation 'io.vproxy:vjson:1.4.3'
    implementation 'com.github.kwhat:jnativehook:2.2.2'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.1.2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/幻塔PC助手-${javafx.platform.classifier}.zip")
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
        jvmArgs = ['-Xmx128m', '-Djava.library.path=%TEMP%']
    }
}

tasks.jlinkZip.doFirst {
    copy {
        from("docs") {
            include "如何运行-How-to-Run.txt"
        }
        into "${buildDir}/image/"
    }
}

jlinkZip {
    group = 'distribution'
}

task runTest(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "net.cassite.hottapcassistant.test.TestMain"
    args = ["net.cassite.hottapcassistant.test." + System.getProperty("TestClass")]
}
